\subsection{Expresiones de Sentencia}

En \textbf{c3P} cada expresión, sentencia o declaración que se codifique se escribe en una sola
línea, por lo que no se pueden representar más de una declaración en una línea. Esto
no afecta a las condiciones y expresiones aritmético/lógicas, pero sí a las estructuras
de control, declaración de funciones y procedimientos, variables, etc. A continuación,
se muestra ejemplos válidos e inválidos de declaraciones.

\begin{verbatim}

? Declaración de expresiones correcta
x : i32 = 10
y : i32 = 20
z : i32 = x + y
z = z + x * 2 / 10
call show "z = "
call showln z

? Declaración de expresiones incorrecta
if (x == y) if (y != 10)

endif endif

? Incorrecto
x : i32 = 10, y = 10
\end{verbatim}

\subsection{Sentencia if}

En \textbf{c3P} se puede utilizar la sentencia «if» para controlar el flujo del programa mediante una operación condicional.

\begin{verbatim}
if (test)
  codigo1
else
  codigo2
endif
\end{verbatim}

Si «test» se evalúa como verdadera, entonces se ejecutará «codigo1» mientras que «codigo2» no lo hará. Si el resultado de la prueba es falso, tendrá lugar el efecto contrario «codigo2» se ejecutará mientras «codigo1» no lo hará. La cláusula «else» es opcional.

Es posible emplear varias sentencias «if» para comprobar múltiples condiciones, como se muestra en el ejemplo:

\begin{verbatim}
if (x == 1)
  call show  ("x es 1")
else if (x == 2)
  call show  ("x es 2")
else if (x == 3)
  call show  ("x es 3")
else
  call show  ("x es otra cosa")
endif
\end{verbatim}

\subsection{Sentencia while}
La declaración «while» es un bucle en el que en cada iteración se comprueba una condición,
que si se satisface, se repite la ejecución del bucle hasta que no se cumpla el criterio
de parada. A continuación se muestra un ejemplo:

\begin{verbatim}
counter : i32 = 0

while (counter < 10)
  call show counter
  counter = counter + 1
endwhile
\end{verbatim}

\subsection{Sentencia for}
El bucle con la declaración «for» es similar al «while», pero con la característica de que
inicializa una variable, que en cada iteración varía, hasta que se cumpla una condición.
La codificación del bucle se hace primero inicializando la variable, luego estableciendo
la condición de parada, y después indicando la expresión que se va a ejecutar para cambiar
la variable tras la iteración. Cada parte del «for» está separado por comas, encerrado en
paréntesis, y el cuerpo del bucle termina en «endfor».

\begin{verbatim}
for (x : i32 = 0, x = x + 1, x < 10)
  call show x
endfor
\end{verbatim}

\subsection{Bloques}
\subsubsection{Sentencia break}

La declaración break se utiliza para terminar la ejecución de un bucle.
Cabe destacar que esta sentencia afecta al bucle más interno en el que esté dentro.
A continuación se muestra un ejemplO:

\begin{verbatim}
for (x : i32 = 1, x = x + 1, x <= 10)
    if (x == 8)
      break
    else
      call show x
    endif
endfor
\end{verbatim}

\subsubsection{Sentencia continue}

La declaración continue permite terminar la iteración actual del bucle y
comenzar la siguiente sin salir de él. Hay que tener en cuenta que esta
declaración sólo afecta al bucle más interno en el que esté dentro.

\begin{verbatim}
suma : i32 = 0

for (x : i32 = 0, x = x + 1, x < 100)
    if (x % 2 == 0)
      continue
    else
      suma = suma + x
    endif
endfor
\end{verbatim}

Si pones una sentencia «continue» dentro de un bucle que a su vez está dentro de un bucle, entonces sólo afecta al bucle más interno.

\subsubsection{Sentencia ret}

La sentencia ret se utiliza para indicar el valor que va a devolver una función al
terminar su ejecución. En \textbf{c3P}, siempre se tiene que devolver en una
función un valor, pero jamás se puede usar en un procedimiento.

\begin{verbatim}
func cumsum : i32(x : i32[])
    value : i32 = 0
    len : i32 = call arrlen x

    for (i : i32 = 0, i = i + 1, i < len)
        value = value + x[i]
    endfor

    ret value
endfunc
\end{verbatim}
